plugins {
    id 'java'
    id 'pl.allegro.tech.build.axion-release' version '1.13.6'
}

project.version = "1.2.7"

sourceCompatibility = 1.8
defaultTasks 'clean', 'build'

configurations{
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    compile{
        extendsFrom pluginLibs
    }
}

repositories {
    mavenCentral()
}

dependencies {
    pluginLibs group: 'org.freemarker', name: 'freemarker', version: '2.3.31'
    compile group: 'org.rundeck', name: 'rundeck-core', version: '3.0.27-20191204'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.35'
}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    //include contents of output dir
    from "$buildDir/output"
    archivesBaseName = "slack-incoming-webhook-plugin"

    manifest {
        attributes 'Rundeck-Plugin-Name' : 'Slack Notification'
        attributes 'Rundeck-Plugin-Description' : 'Sends Rundeck notification messages to a slack channel.'
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '3.0.27+'
        attributes 'Rundeck-Plugin-Tags': 'java,notification,slack'
        attributes 'Rundeck-Plugin-License': 'Apache 2.0'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/stone-payments/slack-incoming-webhook-plugin'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Author': 'Paulo Mariano (paulo.azevedo@stone.com.br)'
        attributes 'Rundeck-Plugin-Version': "1.2", 'Rundeck-Plugin-Archive': 'true','Rundeck-Plugin-File-Version': "${project.version}"
        //create space-separated list of pluginLibs
        def libList = configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')
        attributes 'Rundeck-Plugin-Classnames': 'com.bitplaces.rundeck.plugins.slack.SlackNotificationPlugin', 'Rundeck-Plugin-Libs': "${libList}"
    }
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)

wrapper {
    gradleVersion = '5.6.4'
}
